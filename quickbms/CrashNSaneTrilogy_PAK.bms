# Crash Bandicoot: N-Sane Trilogy / Crash Team Racing Nitro-Fueled PAK extractor
# Original MUA2 script by aluigi (http://quickbms.aluigi.org), hackishly modified (sorry, aluigi) by Random Talking Bush
# Updated Feb. 23, 2021: Fixed 64-bit archive extraction (Crash Team Racing: Nitro-Fueled, PS4 version 1.20's "update2.pak").

get FOLDERNAME basename

get PAKSIZE asize
endian little
idstring "IGA\x1A"
get VER long
get DUMMY long
get FILES long
get UNKNOWN long
get UNKNOWN long
get UNKNOWN long
get ZERO long
get UNKNOWN long
get UNKNOWN long
get NAMES_OFF longlong
get NAMES_SIZE long
get ALWAYS1 long

# get DATA_OFF long

for i = 0 < FILES
    get CRC long
next i
for i = 0 < FILES
    get OFFSET long
    get 64BITFLAG byte
	math 64BITFLAG * 4294967296
	math OFFSET += 64BITFLAG
	get UNKFLAG2 byte
	get UNKFLAG3 byte
	get UNKFLAG4 byte
    get SIZE long
    get MODE long
    putarray 0 i OFFSET
    putarray 1 i SIZE
    putarray 2 i MODE
next i

goto NAMES_OFF
for i = 0 < FILES
    getarray OFFSET 0 i
    getarray SIZE   1 i
    getarray MODE   2 i

#    if OFFSET u>= 0xff000000    # 1006_LightVillain.bld
#        math OFFSET = DATA_OFF
#    endif

    if NAMES_OFF == 0
        set NAME string ""
        savepos BACKUP_OFF
    else
        get NAME_OFF long
        if i == 0
            xmath TMP "FILES * 4"
            if NAME_OFF != TMP
                math CHANGE_ENDIAN = 1
            else
                math CHANGE_ENDIAN = 0
            endif
        endif
        if CHANGE_ENDIAN != 0
            reverselong NAME_OFF
        endif
        savepos BACKUP_OFF
        math NAME_OFF + NAMES_OFF
        goto NAME_OFF
        get EXTNAME string
        get NAME string
    endif

    if MODE == 0xffffffff
		set PATH FOLDERNAME
		string PATH + "/"
		string PATH + NAME
        log PATH OFFSET SIZE
    else
        if MODE & 0x20000000
            comtype lzma_dynamic    # no, it's not just lzma
        else
            comtype deflate_noerror # http://zenhax.com/viewtopic.php?p=11359#p11359
        endif

        putvarchr MEMORY_FILE SIZE 0
        log MEMORY_FILE 0 0
        append
        for MYSIZE = 0 < SIZE
            goto OFFSET
			if VER <= 0x0B
				get ZSIZE short
			else
				get ZSIZE long
			endif
			math ZSIZE + 5
            savepos OFFSET
			get LZMACHECK1 long
			get LZMACHECK2 short
            if LZMACHECK1 == 0x0080005D and LZMACHECK2 == 0x0000
                clog MEMORY_FILE OFFSET ZSIZE 0x8000
            else
				math ZSIZE = 0x8000
				if VER <= 0x0B
					math OFFSET - 2
				else
					math OFFSET - 4
				endif
				set ENDCHECK OFFSET
				math ENDCHECK + ZSIZE
				if ENDCHECK > PAKSIZE # Script errors if it tries to do the usual 0x8000 appending if there's a decompressed chunk at the end of a PAK.
					set ZSIZE PAKSIZE
					math ZSIZE - OFFSET
				endif
				log MEMORY_FILE OFFSET ZSIZE
            endif
            math OFFSET + ZSIZE
            math OFFSET x 0x800
            get MYSIZE asize MEMORY_FILE
        next
        append
		set PATH FOLDERNAME
		string PATH + "/"
		string PATH + NAME
        log PATH 0 SIZE MEMORY_FILE
    endif

    goto BACKUP_OFF
next i
